plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    modApi("dev.architectury:architectury-fabric:${rootProject.architectury_version}") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // for sodium compat
    modRuntimeOnly "maven.modrinth:sodium:mc1.18.2-0.4.1"
    modRuntimeOnly 'org.joml:joml:1.10.2' //temp sodium

    // for iris compat
    modRuntimeOnly "maven.modrinth:iris:1.6.11+1.18.2"
    modRuntimeOnly 'org.anarres:jcpp:1.4.14' //temp iris
    modRuntimeOnly 'io.github.douira:glsl-transformer:2.0.0-pre13' //temp iris

    // iris for immersive poortals
    //modRuntimeOnly "maven.modrinth:iris:1.4.0+1.18.2"
    //modRuntimeOnly 'io.github.douira:glsl-transformer:1.0.1' //temp iris

    // for REI compat
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:8.3.681") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" exclude group: "dev.architectury" }

    // for Pehkui compat
    modCompileOnly("com.github.Virtuoel:Pehkui:3.7.12") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // other mods
    // modRuntimeOnly("com.simibubi.create:create-fabric-1.18.2:0.5.1.c-1159+1.18.2")
    // modCompileOnly "maven.modrinth:lithium:mc1.18.2-0.10.3"
    // modCompileOnly "maven.modrinth:indium:1.0.7+mc1.18.2"

    //mod menu
    modApi("com.terraformersmc:modmenu:3.2.5") { exclude group: "net.fabricmc" exclude group: "net.fabricmc.fabric-api" }

    // for immersive portals compat
    modCompileOnly('com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:v1.4.11-1.18') { transitive false }
    modCompileOnly('com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:v1.4.11-1.18') { transitive false }
    modCompileOnly('com.github.iPortalTeam.ImmersivePortalsMod:build:v1.4.11-1.18') { transitive false }

    // needed for resource loading
    include(modImplementation(fabricApi.module("fabric-resource-loader-v0", rootProject.fabric_api_version)))
    // commands also needs base
    include(modImplementation(fabricApi.module("fabric-api-base", rootProject.fabric_api_version)))
    include(modImplementation(fabricApi.module("fabric-command-api-v1", rootProject.fabric_api_version)))

    include(implementation('com.electronwill.night-config:toml:3.6.6'))
    include(implementation('com.electronwill.night-config:core:3.6.6'))

    include(implementation("org.lwjgl:lwjgl-openvr:3.2.2"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.2.2:natives-linux"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.2.2:natives-macos"))
    include(implementation("org.lwjgl:lwjgl-openvr:3.2.2:natives-windows"))

    include(implementation("QuestCraftPlusPlus:lwjgl3:3.3.3:lwjgl-openxr-3.3.3"))
    include(implementation("org.lwjgl:lwjgl-openxr:3.3.2:natives-linux"))
    include(implementation("org.lwjgl:lwjgl-openxr:3.3.2:natives-windows"))

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set('fabric')
}

jar {
    archiveClassifier.set("dev")
    manifest {
        attributes(
            'Main-Class': "org.vivecraft.LaunchPopup"
        )
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
